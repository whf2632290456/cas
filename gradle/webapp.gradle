description = "Apereo CAS Web Application"

apply plugin: "war"

project.ext["mainClassName"] = "org.apereo.cas.web.CasWebApplication"
sourceSets {
    main {
        resources {
            srcDirs "${project.buildDir}/generated-src/main/resources"
        }
    }
    /**
     * Make resources become reloadable.
     * Applicationâ€™s static resources are loaded from their source location.
     */
    bootRun {
        resources {
            srcDirs new File("//etc/cas/templates/"), new File("${project(":webapp").getProjectDir()}/cas-server-webapp-resources/src/main/resources/")
        }
    }
}

publishing {
    publications {
        mavenWeb(MavenPublication) {
            from components.web
            pom {
                createPom(it, project)
            }
            pom.withXml {
                createPomRepositories(asNode())
            }
        }
    }
}

apply from: rootProject.file("gradle/springboot.gradle")

dependencies {
    implementation project(":core:cas-server-core")
    implementation project(":core:cas-server-core-audit")
    implementation project(":core:cas-server-core-authentication")
    implementation project(":core:cas-server-core-configuration")
    implementation project(":core:cas-server-core-configuration-metadata-repository")
    implementation project(":core:cas-server-core-cookie")
    implementation project(":core:cas-server-core-events")
    implementation project(":core:cas-server-core-events-configuration")
    implementation project(":core:cas-server-core-logout")
    implementation project(":core:cas-server-core-logging")
    implementation project(":core:cas-server-core-monitor")
    implementation project(":core:cas-server-core-services")
    implementation project(":core:cas-server-core-tickets")
    implementation project(":core:cas-server-core-util")
    implementation project(":core:cas-server-core-validation")
    implementation project(":core:cas-server-core-web")

    implementation project(":support:cas-server-support-actions")
    implementation project(":support:cas-server-support-geolocation")
    implementation project(":support:cas-server-support-person-directory")
    implementation project(":support:cas-server-support-pm-webflow")
    implementation project(":support:cas-server-support-themes")
    implementation project(":support:cas-server-support-reports")
    implementation project(":support:cas-server-support-throttle")
    implementation project(":support:cas-server-support-validation")
    
    implementation project(":webapp:cas-server-webapp-config")
    implementation project(":webapp:cas-server-webapp-init")

    implementation libraries.springcloudconfigclient

    developmentOnly libraries.springbootdevtools
    runtimeOnly libraries.webjars
}
